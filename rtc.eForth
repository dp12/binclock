b9 constant twsr
bc constant twcr
bb constant twdr
b8 constant twbr

: twi    ( -- )
    ( Initialize twi interface )
    fc twsr c@ and twsr c! \ Clear prescalar bits
    48 twbr c!             \ Set bit rate for 100kHz
    45 twcr c! ;           \ Set TWEN, TWIE, TWEA

: twint?    ( -- )
    ( Wait for TWINT flag )
    begin twcr c@ 80 and until ;

: start   ( -- )
    ( Send Start condition to TC74 )
    a4 twcr c!
    twint? ;

: ack  c4 twcr c! ;

: nak  84 twcr c! ;

: stop  94 twcr c! ;

: >twi    ( byte -- )
    ( Transmit byte across TWI interface)
    twdr c!                 \ Store byte into data register
    c4 twcr c!              \ Tx byte TWINT and TWEN in TWCR
    twint? ;

: twi>     ( -- byte )
    ( Receive byte from TWI interface and push onto stack)
    twint? twdr c@ ;       \ Load byte from data register

: 2bcd    ( dec -- bcd )
    decimal 10 /MOD 16 * + hex ;

: 2dec    ( bcd -- dec )
    10 /MOD a * + ;

: >time    ( hr min sec --  )
    ( Set HMS time )
    start d0 >twi 00 >twi
    2bcd >twi 2bcd >twi 2bcd >twi
    stop ;

: time?   ( addr -- time )
    start d0 >twi >twi \ Set reg ptr address
    start d1 >twi nak  \ Send read command
    twi> 2dec ;        \ Read seconds

: sec?  0 time? ;
: min?  1 time? ;
: hr?   2 time? ;

